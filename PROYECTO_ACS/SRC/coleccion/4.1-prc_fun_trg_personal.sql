-- Procedimientos, funciones, trigers
-- ! VISTAS MATERIALIZADAS
BEGIN
    EXECUTE IMMEDIATE 'DROP MATERIALIZED VIEW ACS_MV_SOLICITUDES_USUARIOS'; 
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE != -12003 THEN
            RAISE;
        END IF;
END;
/
CREATE MATERIALIZED VIEW ACS_MV_SOLICITUDES_USUARIOS AS
SELECT 
    APE_CEDULA, 
    APE_NOMBRE, 
    APE_P_APELLIDO, 
    APE_S_APELLIDO, 
    -- EDAD EN AÑOS
    FLOOR(MONTHS_BETWEEN(SYSDATE, APE_FECHA_NACIMIENTO) / 12) AS APE_EDAD,
    APE_TIPO_USUARIO, 
    APE_FECHA_CREACION
FROM ACS_PERSONA
WHERE APE_ESTADO_REGISTRO = 'PENDIENTE'
ORDER BY APE_FECHA_CREACION;
/
SELECT * FROM ACS_MV_SOLICITUDES_USUARIOS;

-- ! FUNCIONES
-- FUNCION AUXILIAR PARA EXTRAER CAMPOS DE UNA LINEA CSV
CREATE OR REPLACE FUNCTION UTIL_GET_FIELD(P_LINE VARCHAR2, P_POS NUMBER) RETURN VARCHAR2 IS
    V_START NUMBER := 1;
    V_END NUMBER;
    V_COUNT NUMBER := 1;
    V_CHAR VARCHAR2(1);
BEGIN
    -- Si la posición es 1, buscamos hasta la primera coma
    IF P_POS = 1 THEN
        V_END := INSTR(P_LINE, ',', 1, 1);
        IF V_END = 0 THEN
            RETURN P_LINE;
        ELSE
            RETURN SUBSTR(P_LINE, 1, V_END - 1);
        END IF;
    END IF;

    -- Para posiciones > 1
    FOR I IN 1..LENGTH(P_LINE) LOOP
        V_CHAR := SUBSTR(P_LINE, I, 1);
        IF V_CHAR = ',' THEN
            V_COUNT := V_COUNT + 1;
            IF V_COUNT = P_POS THEN
                V_START := I + 1;
            ELSIF V_COUNT = P_POS + 1 THEN
                V_END := I - 1;
                RETURN SUBSTR(P_LINE, V_START, V_END - V_START + 1);
            END IF;
        END IF;
    END LOOP;

    -- Si es el último campo
    IF V_COUNT = P_POS THEN
        RETURN SUBSTR(P_LINE, V_START);
    END IF;

    RETURN NULL;
END;
/

-- ! PROCEDIMIENTOS
-- PRODECIMIENTO PARA CARGAR UNA SOLICITUD
CREATE OR REPLACE PROCEDURE ACS_PRC_REGISTRO_PERSONA (
    P_APE_CEDULA IN ACS_PERSONA.APE_CEDULA%TYPE,
    P_APE_NOMBRE IN ACS_PERSONA.APE_NOMBRE%TYPE,
    P_APE_P_APELLIDO IN ACS_PERSONA.APE_P_APELLIDO%TYPE,
    P_APE_S_APELLIDO IN ACS_PERSONA.APE_S_APELLIDO%TYPE,
    P_APE_FECHA_NACIMIENTO IN ACS_PERSONA.APE_FECHA_NACIMIENTO%TYPE,
    P_APE_SEXO IN ACS_PERSONA.APE_SEXO%TYPE,
    P_APE_ESTADO_CIVIL IN ACS_PERSONA.APE_ESTADO_CIVIL%TYPE,
    P_APE_NACIONALIDAD IN ACS_PERSONA.APE_NACIONALIDAD%TYPE,
    P_APE_TIPO_USUARIO IN ACS_PERSONA.APE_TIPO_USUARIO%TYPE,
    P_APE_EMAIL IN ACS_PERSONA.APE_EMAIL%TYPE,
    P_APE_TELEFONO IN ACS_PERSONA.APE_TELEFONO%TYPE,
    P_APE_RESIDENCIA IN ACS_PERSONA.APE_RESIDENCIA%TYPE,
    P_APE_DIRECCION_CASA IN ACS_PERSONA.APE_DIRECCION_CASA%TYPE,
    P_APE_DIRECCION_TRABAJO IN ACS_PERSONA.APE_DIRECCION_TRABAJO%TYPE
) AS
BEGIN
    INSERT INTO ACS_PERSONA (
        APE_CEDULA,
        APE_NOMBRE,
        APE_P_APELLIDO,
        APE_S_APELLIDO,
        APE_FECHA_NACIMIENTO,
        APE_SEXO,
        APE_ESTADO_CIVIL,
        APE_NACIONALIDAD,
        APE_TIPO_USUARIO,
        APE_ESTADO_REGISTRO,
        APE_EMAIL,
        APE_TELEFONO,
        APE_RESIDENCIA,
        APE_DIRECCION_CASA,
        APE_DIRECCION_TRABAJO
    ) VALUES (
        P_APE_CEDULA,
        P_APE_NOMBRE,
        P_APE_P_APELLIDO,
        P_APE_S_APELLIDO,
        P_APE_FECHA_NACIMIENTO,
        P_APE_SEXO,
        P_APE_ESTADO_CIVIL,
        P_APE_NACIONALIDAD,
        P_APE_TIPO_USUARIO,
        'PENDIENTE',
        P_APE_EMAIL,
        P_APE_TELEFONO,
        P_APE_RESIDENCIA,
        P_APE_DIRECCION_CASA,
        P_APE_DIRECCION_TRABAJO
    );
END;
/

-- Aprobar o rechazar una solicitud de personal
CREATE OR REPLACE PROCEDURE ACS_PRC_ACTUALIZAR_ESTADO_PERSONA (
    P_APE_CEDULA IN ACS_PERSONA.APE_CEDULA%TYPE,
    P_NEW_ESTADO IN ACS_PERSONA.APE_ESTADO_REGISTRO%TYPE
) AS
BEGIN
    IF P_NEW_ESTADO NOT IN ('APROBADO', 'RECHAZADO') THEN
        RAISE_APPLICATION_ERROR(-20001, '[INFO] Estado inválido. Solo se permiten APROBADO o RECHAZADO.');
    END IF;

    UPDATE ACS_PERSONA
    SET APE_ESTADO_REGISTRO = P_NEW_ESTADO,
        APE_FECHA_ACTUALIZACION = SYSDATE
    WHERE APE_CEDULA = P_APE_CEDULA;
END;
/
-- ! TRIGGERS
-- ** CREA EL USUARIO CUANDO EL ESTADO DEL PERSONA CAMBIA A 'APROBADO', ADEMAS ASIGNA EL PERFIL CORRESPONDIENTE **
CREATE OR REPLACE TRIGGER TRG_AFTER_UPDATE_APE_ESR
FOR UPDATE OF APE_ESTADO_REGISTRO ON ACS_PERSONA
COMPOUND TRIGGER

    -- Colección para almacenar los IDs a procesar
    TYPE T_PERSONA_REC IS RECORD (
        APE_ID ACS_PERSONA.APE_ID%TYPE,
        APE_ESTADO_REGISTRO ACS_PERSONA.APE_ESTADO_REGISTRO%TYPE,
        APE_TIPO_USUARIO ACS_PERSONA.APE_TIPO_USUARIO%TYPE
    );
    
    TYPE T_PERSONA_TAB IS TABLE OF T_PERSONA_REC INDEX BY PLS_INTEGER;
    G_PERSONAS_TO_PROCESS T_PERSONA_TAB;
    G_INDEX PLS_INTEGER := 0;

    -- Fase 1: Capturar los registros modificados
    AFTER EACH ROW IS
    BEGIN
        G_INDEX := G_INDEX + 1;
        G_PERSONAS_TO_PROCESS(G_INDEX).APE_ID := :NEW.APE_ID;
        G_PERSONAS_TO_PROCESS(G_INDEX).APE_ESTADO_REGISTRO := :NEW.APE_ESTADO_REGISTRO;
        G_PERSONAS_TO_PROCESS(G_INDEX).APE_TIPO_USUARIO := :NEW.APE_TIPO_USUARIO;
    END AFTER EACH ROW;

    -- Fase 2: Procesar todos los registros al final
    AFTER STATEMENT IS
        V_NEW_AUS_ID ACS_USUARIO.AUS_ID%TYPE;
    BEGIN
        FOR I IN 1..G_INDEX LOOP
            IF G_PERSONAS_TO_PROCESS(I).APE_ESTADO_REGISTRO = 'APROBADO' THEN
                -- Crear nuevo usuario
                INSERT INTO ACS_USUARIO (APE_ID, AUS_ESTADO, AUS_FECHA_CREACION)
                VALUES (G_PERSONAS_TO_PROCESS(I).APE_ID, 'ACTIVO', SYSDATE)
                RETURNING AUS_ID INTO V_NEW_AUS_ID;

                IF G_PERSONAS_TO_PROCESS(I).APE_TIPO_USUARIO = 'MEDICO' THEN
                    INSERT INTO ACS_MEDICO (AME_ID, AME_ESTADO, AUS_ID)
                    VALUES (V_NEW_AUS_ID, 'ACTIVO', V_NEW_AUS_ID);
                ELSIF G_PERSONAS_TO_PROCESS(I).APE_TIPO_USUARIO = 'ADMINISTRATIVO' THEN
                    INSERT INTO ACS_ADMINISTRATIVO (AAD_ID, AAD_ESTADO, AUS_ID)
                    VALUES (V_NEW_AUS_ID, 'ACTIVO', V_NEW_AUS_ID);
                END IF;
                
            ELSIF G_PERSONAS_TO_PROCESS(I).APE_ESTADO_REGISTRO = 'RECHAZADO' THEN
                -- UN JOB DIARIO BORRARÁ ESTOS REGISTROS, SOLO IMPRIMIR MENSAJE
                DBMS_OUTPUT.PUT_LINE('[INFO] La persona con APE_ID=' || G_PERSONAS_TO_PROCESS(I).APE_ID || ' ha sido rechazada. Será eliminada por el job diario.');
            END IF;
        END LOOP;
        
        -- Limpiar la colección para el próximo ciclo
        G_PERSONAS_TO_PROCESS.DELETE;
        G_INDEX := 0;
    END AFTER STATEMENT;

END TRG_AFTER_UPDATE_APE_ESR;
/

-- ** TRIGGER PARA ACTUALIZAR APE_FECHA_ACTUALIZACION EN ACS_PERSONA **
CREATE OR REPLACE TRIGGER TRG_APE_UPDATE
BEFORE UPDATE ON ACS_PERSONA
FOR EACH ROW
BEGIN
:NEW.APE_FECHA_ACTUALIZACION := SYSDATE;
END;
/

-- ** TRIGGER PARA ACTUALIZAR ADO_FECHA_ACTUALIZACION EN ACS_DOCUMENTO_USUARIO **
CREATE OR REPLACE TRIGGER TRG_ACS_DOU_UPDATE
BEFORE UPDATE ON ACS_DOCUMENTO_USUARIO
FOR EACH ROW
BEGIN
:NEW.ADU_FECHA_ACTUALIZACION := SYSDATE;
END;
/

-- ! JOBS
-- JOB: Ejecuta el procedimiento para borrar usuarios rechazados cada día
BEGIN
    DBMS_SCHEDULER.CREATE_JOB (
        job_name        => 'JOB_BORRAR_PERSONAS_RECHAZADAS',
        job_type        => 'PLSQL_BLOCK',
        job_action      => '
            BEGIN
                FOR r IN (SELECT APE_ID FROM ACS_PERSONA WHERE APE_ESTADO_REGISTRO = ''RECHAZADO'') LOOP
                    ACS_PRC_BORRAR_PERSONA_RECHAZADA(r.APE_ID);
                END LOOP;
            END;
        ',
        start_date      => SYSDATE,
        repeat_interval => 'FREQ=DAILY; BYHOUR=2', -- Ejecuta cada día a las 2am
        enabled         => TRUE
    );
END;
/