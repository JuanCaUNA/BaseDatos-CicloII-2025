-- ! CARGA REGISTROS DEL PADRÓN NACIONAL (TXT) A LA TABLA TEMPORAL PADRON_NACIONAL

-- FORMATO ARCHIVO PADRON_NACIONAL.TXT
-- CEDULA,CODELEC,RELLENO,FECHA_CADUCIDAD,JUNTA,NOMBRE,PRIMER_APELLIDO,SEGUNDO_APELLIDO

-- CREATE DIRECTORY ACS_DIR_PADRON AS 'C:\instaladores\Oracle19c\files\padron_completo';
-- GRANT READ ON DIRECTORY ACS_DIR_PADRON TO JUANC;
--/

-- BORRAR DATOS
BEGIN
    EXECUTE IMMEDIATE 'DELETE FROM ACS_PADRON_NACIONAL';
    COMMIT;
    DBMS_OUTPUT.PUT_LINE('[OK] TABLA ACS_PADRON_NACIONAL LIMPIADA EXITOSAMENTE.');
EXCEPTION
    WHEN OTHERS THEN
        RAISE;
END;
/

-- CARGAR SOLO UNA CANTIDAD N DE REGISTROS
DECLARE
    -- PARAMETROS DE CONFIGURACIÓN
    P_CARGAR_TODO CONSTANT BOOLEAN := FALSE; -- TRUE PARA CARGAR TODO EL ARCHIVO
    P_BLOQUE_SIZE CONSTANT PLS_INTEGER := 100;
    P_CANTIDAD_REGISTROS CONSTANT PLS_INTEGER := 50;
    -- TIPOS
    TYPE T_PADRON IS TABLE OF ACS_PADRON_NACIONAL%ROWTYPE INDEX BY PLS_INTEGER;
    -- variables
    V_PADRON_DATA T_PADRON;
    -- CONTADORES
    V_CONTADOR PLS_INTEGER := 0;
    V_LINEA_ACTUAL PLS_INTEGER := 0;
    -- ARCHIVO
    V_FILE UTL_FILE.FILE_TYPE;
    V_LINE VARCHAR2(120); -- comas (7) + datos (9+6+1+8+5+30+26+26=111) total: 118
BEGIN
    V_FILE := UTL_FILE.FOPEN('ACS_DIR_PADRON', 'PADRON_COMPLETO.txt', 'R');
    V_PADRON_DATA.DELETE;

    LOOP
        EXIT WHEN V_LINEA_ACTUAL >= P_CANTIDAD_REGISTROS AND NOT P_CARGAR_TODO;
        BEGIN
            UTL_FILE.GET_LINE(V_FILE, V_LINE);
        EXCEPTION
            WHEN NO_DATA_FOUND THEN
                EXIT;
        END;

        -- PROCESAR LA LÍNEA Y EXTRAER CAMPOS
        -- Formato archivo:CEDULA,CODELEC,RELLENO,FECHA_CADUCIDAD,JUNTA,NOMBRE,PRIMER_APELLIDO,SEGUNDO_APELLIDO
        -- extrae valor de una celda: UTIL_GET_FIELD(linea, numero_celda)
        V_PADRON_DATA(V_PADRON_DATA.COUNT + 1).APN_CEDULA := UTIL_GET_FIELD(V_LINE, 1);
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_NOMBRE := UTIL_GET_FIELD(V_LINE, 6);
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_P_APELLIDO := UTIL_GET_FIELD(V_LINE, 7);
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_S_APELLIDO := UTIL_GET_FIELD(V_LINE, 8);

        V_CONTADOR := V_CONTADOR + 1;
        V_LINEA_ACTUAL := V_LINEA_ACTUAL + 1;

        -- carga un bloque
        IF V_CONTADOR >= P_BLOQUE_SIZE THEN
            FORALL I IN 1 .. V_PADRON_DATA.COUNT
                INSERT INTO ACS_PADRON_NACIONAL (
                    APN_CEDULA,
                    APN_NOMBRE,
                    APN_P_APELLIDO,
                    APN_S_APELLIDO
                ) VALUES (
                    V_PADRON_DATA(I).APN_CEDULA,
                    V_PADRON_DATA(I).APN_NOMBRE,
                    V_PADRON_DATA(I).APN_P_APELLIDO,
                    V_PADRON_DATA(I).APN_S_APELLIDO
                );
            COMMIT;
            V_PADRON_DATA.DELETE;
            V_CONTADOR := 0;
        END IF;
    END LOOP;

    -- restantes
    IF V_PADRON_DATA.COUNT > 0 THEN
        FORALL I IN 1 .. V_PADRON_DATA.COUNT
            INSERT INTO ACS_PADRON_NACIONAL (
                APN_CEDULA,
                APN_NOMBRE,
                APN_P_APELLIDO,
                APN_S_APELLIDO
            ) VALUES (
                V_PADRON_DATA(I).APN_CEDULA,
                V_PADRON_DATA(I).APN_NOMBRE,
                V_PADRON_DATA(I).APN_P_APELLIDO,
                V_PADRON_DATA(I).APN_S_APELLIDO
            );
        COMMIT;
    END IF;
    UTL_FILE.FCLOSE(V_FILE);
    DBMS_OUTPUT.PUT_LINE('[OK] CARGA COMPLETADA EXITOSAMENTE. REGISTROS CARGADOS: ' || V_LINEA_ACTUAL);
EXCEPTION
    WHEN OTHERS THEN
        IF UTL_FILE.IS_OPEN(V_FILE) THEN
            UTL_FILE.FCLOSE(V_FILE);
        END IF;
        RAISE;
END;
/

-- Consulta 50 registros de la página 1 
SELECT *
FROM ACS_PADRON_NACIONAL
ORDER BY APN_ID
OFFSET (1 - 1) * 50 ROWS FETCH NEXT 50 ROWS ONLY;
