-- ** CONFIGURACIONES DE ENVÍO DE CORREO **

/*
!NOTAS:
    * ENTORNO: ORACLE19C. WINDOWS11

    * CONFIGURACIÓN BREVO:
        - YA CONFIGURADO PARA LOS SIGUIENTES PARAMETROS

    * VARIABLE:
        - USUARIO_PARA_CORREOS: JUAN
*/

--------------------------------------------------------
-- ** SECCIÓN: TABLAS, FUNCIONES, PROCEDIMIENTOS Y PARÁMETROS **

-- ! GESTION DE PARÁMETROS
-- TABLA
/*
BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ACS_PARAMETROS PURGE';
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE != -942 THEN RAISE; END IF; -- IGNORAR SI NO EXISTE
END;
/

CREATE TABLE ACS_PARAMETROS (
    APA_ID      NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    APA_NOMBRE_PARAMETRO   VARCHAR2(50) UNIQUE NOT NULL,
    APA_VALOR_PARAMETRO   VARCHAR2(4000),
    APA_DESCRIPCION       VARCHAR2(200),
    APA_TIPO_PARAMETRO    VARCHAR2(20) DEFAULT 'SISTEMA'  -- OPCIONAL: SISTEMA, EMAIL, SMTP, OTRO
);
/
*/

-- FUNCION
CREATE OR REPLACE FUNCTION ACS_FUN_OBTENER_PARAMETRO(P_NOMBRE_PARAMETRO ACS_PARAMETROS.APA_NOMBRE_PARAMETRO%TYPE)
RETURN VARCHAR2 IS
    V_VALOR ACS_PARAMETROS.APA_VALOR_PARAMETRO%TYPE;
BEGIN
    SELECT APA_VALOR_PARAMETRO INTO V_VALOR
    FROM ACS_PARAMETROS
    WHERE APA_NOMBRE_PARAMETRO = P_NOMBRE_PARAMETRO;

    RETURN V_VALOR;
EXCEPTION
    WHEN NO_DATA_FOUND THEN
        DBMS_OUTPUT.PUT_LINE('[ERROR] (ACS_FUN_OBTENER_PARAMETRO): NO EXISTE EL PARÁMETRO: ' || P_NOMBRE_PARAMETRO);
        RAISE_APPLICATION_ERROR(-20003, '[ERROR] (ACS_FUN_OBTENER_PARAMETRO): NO EXISTE EL PARÁMETRO: ' || P_NOMBRE_PARAMETRO);
END;
/

-- CÓDIGO PARA INSERCIÓN/ACTUALIZACIÓN DE DATOS (SEED DE PARÁMETROS)
MERGE INTO ACS_PARAMETROS t
USING (
    -- Parametro global
    SELECT  'CORREO_DBA' APA_NOMBRE_PARAMETRO, 
            'juancarlos19defebrero@gmail.com' APA_VALOR_PARAMETRO,
            'Correo del DBA' APA_DESCRIPCION,
            'CORREO_DBA' APA_TIPO_PARAMETRO FROM DUAL UNION ALL
    -- Configuracion SMTP
    SELECT 'SMTP_USE_TLS', 'N', 'Indica si se usa STARTTLS (Y/N), N no usa wallet', 'SMTP' FROM DUAL UNION ALL
    SELECT 'SMTP_ACL_FILE', 'brevo_acl.xml', 'Archivo ACL para SMTP', 'SMTP' FROM DUAL UNION ALL
    SELECT 'SMTP_HOST', 'smtp-relay.brevo.com', 'Servidor para envio de correos', 'SMTP' FROM DUAL UNION ALL
    SELECT 'SMTP_PORT', '587', 'Puerto del servidor SMTP', 'SMTP' FROM DUAL UNION ALL
    -- Datos Servidor SMTP
    SELECT 'SMTP_CORREO', 'juan.camacho.solano@est.una.ac.cr', 'Correo registrado para el SMTP', 'SMTP' FROM DUAL UNION ALL
    SELECT 'SMTP_USUARIO', '9a1e38001@smtp-brevo.com', 'Usuario (login) del servidor SMTP', 'SMTP' FROM DUAL
) s
ON (t.APA_NOMBRE_PARAMETRO = s.APA_NOMBRE_PARAMETRO)
WHEN MATCHED THEN
    UPDATE SET
        t.APA_VALOR_PARAMETRO = s.APA_VALOR_PARAMETRO,
        t.APA_DESCRIPCION = s.APA_DESCRIPCION,
        t.APA_TIPO_PARAMETRO = s.APA_TIPO_PARAMETRO
WHEN NOT MATCHED THEN
    INSERT (APA_NOMBRE_PARAMETRO, APA_VALOR_PARAMETRO, APA_DESCRIPCION, APA_TIPO_PARAMETRO)
    VALUES (s.APA_NOMBRE_PARAMETRO, s.APA_VALOR_PARAMETRO, s.APA_DESCRIPCION, s.APA_TIPO_PARAMETRO);
COMMIT;
/

-- !CLAVES
/*
BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ACS_CLAVE PURGE';
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE != -942 THEN RAISE; END IF; -- IGNORAR SI NO EXISTE
END;
/

-- TABLA
CREATE TABLE ACS_CLAVE (
    ACL_ID       NUMBER GENERATED BY DEFAULT AS IDENTITY,
    ACL_NOMBRE_CLAVE VARCHAR2(50) UNIQUE NOT NULL,
    ACL_TIPO     VARCHAR2(20) NOT NULL, -- CORREO SMTP, OTRO
    ACL_CLAVE    RAW(400) NOT NULL
);
/
*/

-- PRCEDIMIENTO
CREATE OR REPLACE PROCEDURE ACS_PRC_GUARDAR_CLAVE(
    P_NOMBRE_CLAVE IN ACS_CLAVE.ACL_NOMBRE_CLAVE%TYPE,
    P_TIPO IN ACS_CLAVE.ACL_TIPO%TYPE,
    P_CLAVE IN VARCHAR2
) AS
    V_CLAVE_ENCRIPTADA ACS_CLAVE.ACL_CLAVE%TYPE;
    V_KEY_RAW RAW(100);
    V_IV RAW(16);
    V_CIPH_RAW RAW(4000);
BEGIN
    -- OBTENER LA CLAVE Y CONVERTIRLA A RAW
    V_KEY_RAW := UTL_RAW.CAST_TO_RAW('12345678901234567890123456789012'); -- 32 BYTES (AES-256)

    -- GENERAR IV DE 16 BYTES PARA AES-CBC
    V_IV := DBMS_CRYPTO.RANDOMBYTES(16);

    -- ENCRIPTAR LA CLAVE CON AES-256-CBC + PKCS5 Y PREFIJAR EL IV AL CIPHERTEXT
    V_CIPH_RAW := DBMS_CRYPTO.ENCRYPT(
        SRC => UTL_RAW.CAST_TO_RAW(P_CLAVE),
        TYP => DBMS_CRYPTO.ENCRYPT_AES256 + DBMS_CRYPTO.CHAIN_CBC + DBMS_CRYPTO.PAD_PKCS5,
        KEY => V_KEY_RAW,
        IV  => V_IV
    );
    V_CLAVE_ENCRIPTADA := V_IV || V_CIPH_RAW; -- ALMACENAR IV || CIPHERTEXT

    -- ACTUALIZAR O INSERTAR
    MERGE INTO ACS_CLAVE T
    USING (SELECT P_NOMBRE_CLAVE AS ACL_NOMBRE_CLAVE, P_TIPO AS ACL_TIPO FROM DUAL) S
    ON (T.ACL_NOMBRE_CLAVE = S.ACL_NOMBRE_CLAVE AND T.ACL_TIPO = S.ACL_TIPO)
    WHEN MATCHED THEN
        UPDATE SET T.ACL_CLAVE = V_CLAVE_ENCRIPTADA
    WHEN NOT MATCHED THEN
        INSERT (ACL_NOMBRE_CLAVE, ACL_TIPO, ACL_CLAVE)
        VALUES (P_NOMBRE_CLAVE, P_TIPO, V_CLAVE_ENCRIPTADA);

EXCEPTION
    WHEN OTHERS THEN
        RAISE_APPLICATION_ERROR(-20001, '[ERROR] (ACS_PRC_GUARDAR_CLAVE): ' || SQLERRM);
END ACS_PRC_GUARDAR_CLAVE;
/

-- FUNCION
CREATE OR REPLACE FUNCTION ACS_FUN_OBTENER_CLAVE(
    P_NOMBRE_CLAVE IN ACS_CLAVE.ACL_NOMBRE_CLAVE%TYPE
) RETURN VARCHAR2 IS
    V_CLAVE_ENCRIPTADA ACS_CLAVE.ACL_CLAVE%TYPE;
    V_CLAVE_DESENCRIPTADA VARCHAR2(100);
    V_KEY_RAW RAW(100);
    V_DECRYPTED_RAW RAW(4000);
    V_IV RAW(16); -- ES EL IV (VECTOR DE INICIALIZACIÓN)
    V_CIPH_RAW RAW(4000); -- ES EL CIPHERTEXT (TEXTO CIFRADO)
BEGIN
    -- OBTENER LA CLAVE Y CONVERTIRLA A RAW
    V_KEY_RAW := UTL_RAW.CAST_TO_RAW('12345678901234567890123456789012'); -- 32 BYTES (AES-256)

    -- RECUPERAR LA CLAVE ENCRIPTADA
    SELECT ACL_CLAVE INTO V_CLAVE_ENCRIPTADA
    FROM ACS_CLAVE
    WHERE ACL_NOMBRE_CLAVE = P_NOMBRE_CLAVE;

    -- DESENCRIPTAR LA CLAVE
    -- REGLA: SI EL TAMAÑO ES > 16 BYTES, ASUMIMOS FORMATO NUEVO (IV||CIPH) CON AES-256-CBC.
    -- EN CASO CONTRARIO, INTENTAMOS COMPATIBILIDAD CON EL FORMATO LEGADO DES-CBC-PKCS5 SIN IV.
    IF LENGTH(V_CLAVE_ENCRIPTADA) > 16 THEN
        -- EXTRAER IV (PRIMEROS 16 BYTES) Y CIPHERTEXT (RESTO)
        V_IV := UTL_RAW.SUBSTR(V_CLAVE_ENCRIPTADA, 1, 16);
        V_CIPH_RAW := UTL_RAW.SUBSTR(V_CLAVE_ENCRIPTADA, 17);
        V_DECRYPTED_RAW := DBMS_CRYPTO.DECRYPT(
            SRC => V_CIPH_RAW,
            TYP => DBMS_CRYPTO.ENCRYPT_AES256 + DBMS_CRYPTO.CHAIN_CBC + DBMS_CRYPTO.PAD_PKCS5,
            KEY => V_KEY_RAW,
            IV  => V_IV
        );
    ELSE
        -- COMPATIBILIDAD HACIA ATRÁS (VALORES ANTIGUOS DES_CBC_PKCS5 SIN IV)
        V_DECRYPTED_RAW := DBMS_CRYPTO.DECRYPT(
            SRC => V_CLAVE_ENCRIPTADA,
            TYP => DBMS_CRYPTO.DES_CBC_PKCS5,
            KEY => SUBSTR(V_KEY_RAW, 1, 8) -- DES NECESITA 8 BYTES
        );
    END IF;
    
    -- CONVERTIR RAW A VARCHAR2
    V_CLAVE_DESENCRIPTADA := UTL_RAW.CAST_TO_VARCHAR2(V_DECRYPTED_RAW);

    RETURN V_CLAVE_DESENCRIPTADA;
EXCEPTION
    WHEN NO_DATA_FOUND THEN
        DBMS_OUTPUT.PUT_LINE('[ERROR] (ACS_FUN_OBTENER_CLAVE): No existe la clave: ' || P_NOMBRE_CLAVE);
        RAISE_APPLICATION_ERROR(-20004, '[ERROR] (ACS_FUN_OBTENER_CLAVE): No existe la clave: ' || P_NOMBRE_CLAVE);
    WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE('[ERROR] (ACS_FUN_OBTENER_CLAVE): ' || SQLERRM);
        RAISE_APPLICATION_ERROR(-20002, '[ERROR] (ACS_FUN_OBTENER_CLAVE): ' || SQLERRM);
END ACS_FUN_OBTENER_CLAVE;
/

-- CLAVE USUARIO SERVIDOR SMTP
BEGIN
    ACS_PRC_GUARDAR_CLAVE('SMTP_CLAVE', 'SMTP', 'bskiTLMr8wV26Rp');
END;
/

-- ------------------------------------------------------------
-- ** Sección: Configuración ACL para envío de correos vía SMTP **

/*
    ACL (ACCESS CONTROL LIST) ES UN CONJUNTO DE REGLAS QUE DEFINEN LOS PERMISOS DE ACCESO A RECURSOS DE RED ESPECÍFICOS PARA USUARIOS O ROLES EN ORACLE DATABASE.
    ESTOS PERMISOS SON NECESARIOS PARA QUE LOS PROCEDIMIENTOS ALMACENADOS PUEDAN INTERACTUAR CON SERVICIOS EXTERNOS, COMO SERVIDORES SMTP PARA EL ENVÍO DE CORREOS ELECTRÓNICOS.
*/

DECLARE
    V_ACL_FILE VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_ACL_FILE');
BEGIN
    -- * PASO 1: LIMPIAR ACLS ANTERIORES *
    DBMS_NETWORK_ACL_ADMIN.DROP_ACL(ACL => V_ACL_FILE);
    DBMS_OUTPUT.PUT_LINE('[OK] ACL ANTERIOR ELIMINADO');
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE = -46104 THEN
            DBMS_OUTPUT.PUT_LINE('[INFO] NO EXISTÍA ACL PREVIO');
        ELSE
            DBMS_OUTPUT.PUT_LINE('[WARN] ERROR ELIMINANDO ACL: ' || SQLERRM);
        END IF;
END;
/

DECLARE
    V_ACL_FILE VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_ACL_FILE');
    V_PORT NUMBER := TO_NUMBER(ACS_FUN_OBTENER_PARAMETRO('SMTP_PORT'));
    V_HOST VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_HOST');
BEGIN
-- * PASO 2: CREAR ACL Y ASIGNAR PRIVILEGIOS A UN USUARIO *
    DBMS_NETWORK_ACL_ADMIN.CREATE_ACL(
        acl         => V_ACL_FILE,
        description => 'Acceso SMTP ' || V_HOST || ':' || V_PORT,
        principal   => 'JUAN',
        is_grant    => TRUE,
        privilege   => 'connect'
    );
    DBMS_OUTPUT.PUT_LINE('[OK] ACL creado con privilegio CONNECT');

    DBMS_NETWORK_ACL_ADMIN.ADD_PRIVILEGE(
        acl       => V_ACL_FILE,
        principal => 'JUAN',
        is_grant  => TRUE,
        privilege => 'resolve'
    );
    DBMS_OUTPUT.PUT_LINE('[OK] Privilegio RESOLVE agregado');

    -- * PASO 3: ASIGNAR ACL A HOST SMTP *
    DBMS_NETWORK_ACL_ADMIN.ASSIGN_ACL(
        acl  => V_ACL_FILE,
        host => V_HOST,
        lower_port => V_PORT,
        upper_port => V_PORT
    );
    DBMS_OUTPUT.PUT_LINE('[OK] ACL asignado a ' || V_HOST || ':' || V_PORT);
    COMMIT;
END;
/

-- ------------------------------------------------------------
-- ** Sección: Procedimiento almacenado para envío de correos vía SMTP **

-- *  PROCEDIMIENTO ALMACENADO DE ENVIO DE CORREO NOTIFICADOR*
CREATE OR REPLACE PROCEDURE ACS_PRC_CORREO_NOTIFICADOR (
    P_DESTINATARIO IN VARCHAR2,
    P_ASUNTO IN VARCHAR2,
    P_MENSAJE IN VARCHAR2
) 
IS
    -- CONEXION
    MAIL_CONN UTL_SMTP.CONNECTION;
    -- COSTANTES
    CRLF CONSTANT VARCHAR2(2) := UTL_TCP.CRLF;
    -- PARAMETROS
    P_SMTP_USE_TLS VARCHAR2(1) := ACS_FUN_OBTENER_PARAMETRO('SMTP_USE_TLS');
    P_SMTP_HOST VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_HOST');
    P_SMTP_PORT NUMBER := TO_NUMBER(ACS_FUN_OBTENER_PARAMETRO('SMTP_PORT'));
    P_SMTP_USERNAME VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_USUARIO');
    P_REMITENTE VARCHAR2(100) := ACS_FUN_OBTENER_PARAMETRO('SMTP_CORREO');
    P_CLAVE_SMTP VARCHAR2(100) := ACS_FUN_OBTENER_CLAVE('SMTP_CLAVE');
    -- VARIABLES
    V_LISTA_DESTINATARIOS SYS.ODCIVARCHAR2LIST := SYS.ODCIVARCHAR2LIST();
BEGIN
    -- CONTEO
    V_LISTA_DESTINATARIOS := SYS.ODCIVARCHAR2LIST();
    FOR i IN 1 .. REGEXP_COUNT(P_DESTINATARIO, '[^,]+') LOOP
        V_LISTA_DESTINATARIOS.EXTEND;
        V_LISTA_DESTINATARIOS(i) := TRIM(REGEXP_SUBSTR(P_DESTINATARIO, '[^,]+', 1, i));
    END LOOP;
    -- CHECK
    IF V_LISTA_DESTINATARIOS.COUNT = 0 THEN
        DBMS_OUTPUT.PUT_LINE('[ERROR] (ACS_PRC_CORREO_NOTIFICADOR): DEBE INDICAR AL MENOS UN DESTINATARIO.');
        RAISE_APPLICATION_ERROR(-20000, '[ERROR] (ACS_PRC_CORREO_NOTIFICADOR): DEBE INDICAR AL MENOS UN DESTINATARIO.');
    END IF;

    -- CONECTAR AL SERVIDOR SMTP
    MAIL_CONN := UTL_SMTP.OPEN_CONNECTION(P_SMTP_HOST, P_SMTP_PORT);
    UTL_SMTP.EHLO(MAIL_CONN, P_SMTP_HOST);

    -- STARTTLS OPCIONAL (POR DEFECTO DESACTIVADO PARA EVITAR ORA-29024 SI NO HAY WALLET CONFIGURADO)
    IF UPPER(P_SMTP_USE_TLS) = 'Y' THEN
        UTL_SMTP.STARTTLS(MAIL_CONN); -- REQUIERE WALLET/CA CONFIGURADO A NIVEL DE BASE
        UTL_SMTP.EHLO(MAIL_CONN, P_SMTP_HOST);
    END IF;

    -- Autenticación
    UTL_SMTP.AUTH(
        c        => MAIL_CONN,
        username => P_SMTP_USERNAME,
        password => P_CLAVE_SMTP,
        schemes  => UTL_SMTP.ALL_SCHEMES
    );

    -- DE: REMITENTE  A DESTINATARIO
    UTL_SMTP.MAIL(MAIL_CONN, P_REMITENTE);
    -- TO por cada destinatario
    FOR i IN 1 .. V_LISTA_DESTINATARIOS.COUNT LOOP
        UTL_SMTP.RCPT(MAIL_CONN, V_LISTA_DESTINATARIOS(i));
    END LOOP;

    -- CONTENIDO DEL CORREO
    UTL_SMTP.OPEN_DATA(MAIL_CONN);
    
    UTL_SMTP.WRITE_DATA(MAIL_CONN,
        'From: ' || P_REMITENTE || CRLF ||
        'To: ' || P_DESTINATARIO || CRLF ||
        'Subject: ' || P_ASUNTO || CRLF ||
        'MIME-Version: 1.0' || CRLF ||
        'Content-Type: text/html; charset=UTF-8' || CRLF ||
        'Content-Transfer-Encoding: 8bit' || CRLF ||
        CRLF ||
        P_MENSAJE
    );

    UTL_SMTP.CLOSE_DATA(MAIL_CONN);
    UTL_SMTP.QUIT(MAIL_CONN);

    DBMS_OUTPUT.PUT_LINE('[OK] CORREO(S) ENVIADO CORRECTAMENTE');
EXCEPTION
    WHEN OTHERS THEN
        BEGIN
            UTL_SMTP.QUIT(MAIL_CONN);
        EXCEPTION
            WHEN OTHERS THEN
                DBMS_OUTPUT.PUT_LINE('[WARN] (ACS_PRC_CORREO_NOTIFICADOR): NO SE PUDO CERRAR LA CONEXIÓN SMTP: ' || SQLERRM);
        END;
        DBMS_OUTPUT.PUT_LINE('[ERROR] (ACS_PRC_CORREO_NOTIFICADOR): AL ENVIAR CORREO: ' || SQLERRM);
        RAISE_APPLICATION_ERROR(-20001, '[ERROR] (ACS_PRC_CORREO_NOTIFICADOR): AL ENVIAR CORREO: ' || SQLERRM);
END;
/