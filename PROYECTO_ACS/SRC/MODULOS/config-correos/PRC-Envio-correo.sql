-- ** Seccion: Procedimiento almacenado para envio de correos via SMTP **

-- * PASO 6: PROCEDIMIENTO ALMACENADO DE ENVIO DE CORREO NOTIFICADOR  puerto 587*
CREATE OR REPLACE PROCEDURE ACS_PRC_CORREO_NOTIFICADOR (
    P_DESTINATARIO IN VARCHAR2,
    P_ASUNTO IN VARCHAR2,
    P_MENSAJE IN VARCHAR2
) 
IS
    -- CONEXION
    MAIL_CONN UTL_SMTP.CONNECTION;
    -- COSTANTES
    CRLF CONSTANT VARCHAR2(2) := UTL_TCP.CRLF;
    -- PARAMETROS
    P_SMTP_HOST ACS_PARAMETROS.APA_NOMBRE_PARAMETRO%TYPE := ACS_FN_OBTENER_PARAMETRO('SMTP_HOST');
    P_SMTP_PORT NUMBER := TO_NUMBER(ACS_FN_OBTENER_PARAMETRO('SMTP_PORT'));
    P_REMITENTE ACS_PARAMETROS.APA_NOMBRE_PARAMETRO%TYPE := ACS_FN_OBTENER_PARAMETRO('SMTP_CORREO');
    P_CLAVE_SMTP ACS_CLAVE.ACL_NOMBRE_CLAVE%TYPE := ACS_FUN_OBTENER_CLAVE('SMTP_CLAVE');
    -- VARIABLES
    V_LISTA_DESTINATARIOS SYS.ODCIVARCHAR2LIST := SYS.ODCIVARCHAR2LIST();
BEGIN
    -- CONTEO
    V_LISTA_DESTINATARIOS := SYS.ODCIVARCHAR2LIST();
    FOR i IN 1 .. REGEXP_COUNT(P_DESTINATARIO, '[^,]+') LOOP
        V_LISTA_DESTINATARIOS.EXTEND;
        V_LISTA_DESTINATARIOS(i) := TRIM(REGEXP_SUBSTR(P_DESTINATARIO, '[^,]+', 1, i));
    END LOOP;
    -- CHECK
    IF V_LISTA_DESTINATARIOS.COUNT = 0 THEN
        RAISE_APPLICATION_ERROR(-20000, 'Debe indicar al menos un destinatario.');
    END IF;

    -- CONECTAR CON Brevo, LOCAL SIN TLS
    MAIL_CONN := UTL_SMTP.OPEN_CONNECTION(P_SMTP_HOST, P_SMTP_PORT);
    
    UTL_SMTP.HELO(MAIL_CONN, P_SMTP_HOST);
    UTL_SMTP.STARTTLS(MAIL_CONN); -- Iniciar TLS, 587
    UTL_SMTP.EHLO(MAIL_CONN, P_SMTP_HOST);

    -- Autenticación
    UTL_SMTP.AUTH(
        c        => MAIL_CONN,
        username => P_REMITENTE,
        password => P_CLAVE_SMTP,
        schemes  => UTL_SMTP.ALL_SCHEMES
    );

    -- DE: REMITENTE  A DESTINATARIO
    UTL_SMTP.MAIL(MAIL_CONN, P_REMITENTE);
    -- TO por cada destinatario
    FOR i IN 1 .. V_LISTA_DESTINATARIOS.COUNT LOOP
        UTL_SMTP.RCPT(MAIL_CONN, V_LISTA_DESTINATARIOS(i));
    END LOOP;

    -- CONTENIDO DEL CORREO
    UTL_SMTP.OPEN_DATA(MAIL_CONN);
    
    UTL_SMTP.WRITE_DATA(MAIL_CONN,
        'From: ' || P_REMITENTE || CRLF ||
        'To: ' || P_DESTINATARIO || CRLF ||
        'Subject: ' || P_ASUNTO || CRLF ||
        'MIME-Version: 1.0' || CRLF ||
        'Content-Type: text/html; charset=UTF-8' || CRLF ||
        'Content-Transfer-Encoding: 8bit' || CRLF ||
        CRLF ||
        P_MENSAJE
    );

    UTL_SMTP.CLOSE_DATA(MAIL_CONN);
    UTL_SMTP.QUIT(MAIL_CONN);

    DBMS_OUTPUT.PUT_LINE('[OK] Correo(s) enviado correctamente');
EXCEPTION
    WHEN OTHERS THEN
        BEGIN
            IF MAIL_CONN IS NOT NULL THEN
                UTL_SMTP.QUIT(MAIL_CONN);
            END IF;
        EXCEPTION
            WHEN OTHERS THEN
                NULL;
        END;
        DBMS_OUTPUT.PUT_LINE('[ERROR] al enviar correo (ACS_PRC_CORREO_NOTIFICADOR): ' || SQLERRM);
        RAISE_APPLICATION_ERROR(-20001, '[ERROR] al enviar correo (ACS_PRC_CORREO_NOTIFICADOR): ' || SQLERRM);
END;
/

---------------
/*
BEGIN
ACS_PRC_CORREO_NOTIFICADOR(
    P_DESTINATARIO => 'persona@dominio.com',
    P_ASUNTO       => 'Prueba única',
    P_MENSAJE      => '<h1>Hola</h1><p>Mensaje de prueba.</p>'
);
END;
/

BEGIN
ACS_PRC_CORREO_NOTIFICADOR(
    P_DESTINATARIO => 'uno@dominio.com,dos@dominio.com,tres@dominio.com',
    P_ASUNTO       => 'Prueba múltiple',
    P_MENSAJE      => '<p>Hola a todos.</p>'
);
END;
/
*/