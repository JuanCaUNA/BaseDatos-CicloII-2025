-- ! CARGA REGISTROS DEL PADRÓN NACIONAL (TXT) A LA TABLA TEMPORAL PADRON_NACIONAL

-- FORMATO ARCHIVO PADRON_NACIONAL.TXT
-- CEDULA,CODELEC,RELLENO,FECHA_CADUCIDAD,JUNTA,NOMBRE,PRIMER_APELLIDO,SEGUNDO_APELLIDO

BEGIN
    EXECUTE IMMEDIATE 'DROP TABLE ACS_PADRON_NACIONAL PURGE';
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE != -942 THEN RAISE; END IF; -- IGNORAR SI NO EXISTE
END;
/

CREATE TABLE ACS_PADRON_NACIONAL (
    APN_ID      NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    APN_CEDULA  VARCHAR2(9) UNIQUE NOT NULL,
    APN_NOMBRE  VARCHAR2(30) NOT NULL,
    APN_PRIMER_APELLIDO VARCHAR2(26) NOT NULL,
    APN_SEGUNDO_APELLIDO VARCHAR2(26) NOT NULL
)
/

/*
CREATE DIRECTORY ACS_DIR_PADRON AS 'C:\instaladores\Oracle19c\files\padron_completo';
/
GRANT READ ON DIRECTORY ACS_DIR_PADRON TO JUANC;
/
*/

-- CARGAR SOLO UNA CANTIDAD N DE REGISTROS
DECLARE
    -- PARAMETROS DE CONFIGURACIÓN
    P_BLOQUE_SIZE CONSTANT PLS_INTEGER := 500;
    P_CANTIDAD_REGISTROS CONSTANT PLS_INTEGER := 100;
    -- TIPOS
    TYPE T_PADRON IS TABLE OF ACS_PADRON_NACIONAL%ROWTYPE;
    -- VARIABLES
    V_PADRON_DATA T_PADRON;
    V_CEDULA ACS_PADRON_NACIONAL.APN_CEDULA%TYPE;
    V_NOMBRE ACS_PADRON_NACIONAL.APN_NOMBRE%TYPE;
    V_PRIMER_APELLIDO ACS_PADRON_NACIONAL.APN_PRIMER_APELLIDO%TYPE;
    V_SEGUNDO_APELLIDO ACS_PADRON_NACIONAL.APN_SEGUNDO_APELLIDO%TYPE;
    -- CONTADORES
    V_CONTADOR PLS_INTEGER := 0;
    V_LINEA_ACTUAL PLS_INTEGER := 0;
    -- ARCHIVO
    V_FILE UTL_FILE.FILE_TYPE;
    V_LINE VARCHAR2(120); -- comas (7) + datos (9+6+1+8+5+30+26+26=111) total: 118
BEGIN
    -- ABRIR EL ARCHIVO EN MODO LECTURA (DESDE EL DIRECTORIO ORACLE)
    V_FILE := UTL_FILE.FOPEN('ACS_DIR_PADRON', 'PADRON_COMPLETO.txt', 'R');

    -- INICIALIZAR LA COLECCIÓN
    V_PADRON_DATA := T_PADRON();

    LOOP
        EXIT WHEN V_LINEA_ACTUAL >= P_CANTIDAD_REGISTROS;
        BEGIN
            UTL_FILE.GET_LINE(V_FILE, V_LINE);
        EXCEPTION
            WHEN NO_DATA_FOUND THEN
                EXIT; -- SALIR DEL BUCLE AL FINAL DEL ARCHIVO
        END;

        -- PROCESAR LA LÍNEA Y EXTRAER CAMPOS
        -- Formato archivo:CEDULA,CODELEC,RELLENO,FECHA_CADUCIDAD,JUNTA,NOMBRE,PRIMER_APELLIDO,SEGUNDO_APELLIDO
        -- Estructura REGEXP_SUBSTR: (cadena, patrón, posición_inicio, ocurrencia)
        -- tipos de patrones:
        --  [^,]+  : cualquier caracter excepto coma (una o más veces)
        -- Esructura TRIM: elimina espacios en blanco al inicio y final
        -- posicion 5 (comas antes del nombre)
        V_CEDULA := TRIM(REGEXP_SUBSTR(V_LINE, '[^,]+', 1, 1));
        V_NOMBRE := TRIM(REGEXP_SUBSTR(V_LINE, '[^,]+', 1, 6));
        V_PRIMER_APELLIDO := TRIM(REGEXP_SUBSTR(V_LINE, '[^,]+', 1, 7));
        V_SEGUNDO_APELLIDO := TRIM(REGEXP_SUBSTR(V_LINE, '[^,]+', 1, 8));

        -- AGREGAR A LA COLECCIÓN
        V_PADRON_DATA.EXTEND;
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_CEDULA := V_CEDULA;
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_NOMBRE := V_NOMBRE;
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_PRIMER_APELLIDO := V_PRIMER_APELLIDO;
        V_PADRON_DATA(V_PADRON_DATA.COUNT).APN_SEGUNDO_APELLIDO := V_SEGUNDO_APELLIDO;

        V_CONTADOR := V_CONTADOR + 1;
        V_LINEA_ACTUAL := V_LINEA_ACTUAL + 1;

        -- INSERTAR EN BLOQUES
        IF V_CONTADOR >= P_BLOQUE_SIZE THEN
            FORALL I IN 1 .. V_PADRON_DATA.COUNT
                INSERT INTO ACS_PADRON_NACIONAL (
                    APN_CEDULA,
                    APN_NOMBRE,
                    APN_PRIMER_APELLIDO,
                    APN_SEGUNDO_APELLIDO
                ) VALUES (
                    V_PADRON_DATA(I).APN_CEDULA,
                    V_PADRON_DATA(I).APN_NOMBRE,
                    V_PADRON_DATA(I).APN_PRIMER_APELLIDO,
                    V_PADRON_DATA(I).APN_SEGUNDO_APELLIDO
                );
            COMMIT;
            V_PADRON_DATA.DELETE;
            V_CONTADOR := 0;
        END IF;
    END LOOP;

    -- INSERTAR LOS REGISTROS RESTANTES
    IF V_PADRON_DATA.COUNT > 0 THEN
        FORALL I IN 1 .. V_PADRON_DATA.COUNT
            INSERT INTO ACS_PADRON_NACIONAL (
                APN_CEDULA,
                APN_NOMBRE,
                APN_PRIMER_APELLIDO,
                APN_SEGUNDO_APELLIDO
            ) VALUES (
                V_PADRON_DATA(I).APN_CEDULA,
                V_PADRON_DATA(I).APN_NOMBRE,
                V_PADRON_DATA(I).APN_PRIMER_APELLIDO,
                V_PADRON_DATA(I).APN_SEGUNDO_APELLIDO
            );
        COMMIT;
    END IF;
    UTL_FILE.FCLOSE(V_FILE);
    DBMS_OUTPUT.PUT_LINE('[OK] CARGA COMPLETADA EXITOSAMENTE. REGISTROS CARGADOS: ' || V_LINEA_ACTUAL);
EXCEPTION
    WHEN OTHERS THEN
        IF UTL_FILE.IS_OPEN(V_FILE) THEN
            UTL_FILE.FCLOSE(V_FILE);
        END IF;
        RAISE;
END;
/

-- Consulta 50 registros de la página 1 
SELECT *
FROM ACS_PADRON_NACIONAL
ORDER BY APN_ID
OFFSET (1 - 1) * 50 ROWS FETCH NEXT 50 ROWS ONLY;
